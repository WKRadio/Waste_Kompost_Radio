declare name "WKR Library";
declare author "Luca Spanedda";
declare version "1.0";
declare description "2023";
declare copyright "Copyright (C) 2023 Luca Spanedda <lucaspanedda1995@gmail.com>";
declare license "MIT license";
// Import the standard Faust Libraries
import("stdfaust.lib");


/************************************************************************
************************************************************************
- BASIC FUNCTIONS -
************************************************************************
************************************************************************/

//---------------------------------------------------------------------- MATH --
// PI Constant
pi = 3.141592653589793238462643383279502884197169399375105820974944592307816406;

// PI Constant * 2
twopi = pi * 2;

// Epsilon Constant
epsilon = 2.2204460492503131e-016;

// MAX Constant
maximum = 1.7976931348623158e+308;

// MIN Constant
minimum = 2.2250738585072014e-308;

// only decimal pass
decimal(x) = x - int(x);

// only integer pass
integer(x) = int(x);

// shift in phase (0 to 1) giving only the fractional part of x
wrap(ph, x) = ((x + ph) : \(y).(y - int(y)));
// process = os.phasor(1, 400) <: _, wrap(.5);

// Sine window function
sine(ph) =   sin(ph * 2 * pi);

// Cosine window function
cosine(ph) = cos((ph + .25) * 2 * pi);
// process = os.phasor(1, 400) <: sine, cosine;

// Hann window function
hann(ph) = sin(pi * ph) ^ 2.0;
//process = os.phasor(1, 100) <: hann, hann(wrap(.5));

// Sinc window function
sinc(power, ph) = (sin(twopi * ph * power) / sin(twopi * ph) / power);
// process = (os.phasor(1, 122) : sinc(800)), (os.phasor(1, 120) : sinc(800));

// TanH window function
tanh(g, ph) = ma.tanh(ph * g);
// process = os.osc(200) : tanh(100);

// triangular function
triangular(ph) = abs(ma.frac((ph - .5)) * 2.0 - 1.0);
// process = os.phasor(1, 100) : triangular;

// softclipping via hyperbolic tangent function
saturator(lim, x) = lim * tanh(1, x / (max(lim, ma.EPSILON)));
//process = os.osc(200) : saturator(10);

// random: hange the seed value for generate a different random number
random(seed)  = ((seed*(1103515245)))/2147483647.0;
// process = random(12);

// Prime Numbers List
primes =
(2, 3, 5, 7, 11, 13, 17, 19, 23, 29, 31, 37, 41, 43, 47, 53, 59, 61, 67, 71, 73,
79, 83, 89, 97, 101, 103, 107, 109, 113, 127, 131, 137, 139, 149, 151, 157, 163,
167, 173, 179, 181, 191, 193, 197, 199, 211, 223, 227, 229, 233, 239, 241, 251,
257, 263, 269, 271, 277, 281, 283, 293, 307, 311, 313, 317, 331, 337, 347, 349,
353, 359, 367, 373, 379, 383, 389, 397, 401, 409, 419, 421, 431, 433, 439, 443,
449, 457, 461, 463, 467, 479, 487, 491, 499, 503, 509, 521, 523, 541, 547, 557,
563, 569, 571, 577, 587, 593, 599, 601, 607, 613, 617, 619, 631, 641, 643, 647,
653, 659, 661, 673, 677, 683, 691, 701, 709, 719, 727, 733, 739, 743, 751, 757,
761, 769, 773, 787, 797, 809, 811, 821, 823, 827, 829, 839, 853, 857, 859, 863,
877, 881, 883, 887, 907, 911, 919, 929, 937, 941, 947, 953, 967, 971, 977, 983,
991, 997, 1009, 1013, 1019, 1021, 1031, 1033, 1039, 1049, 1051, 1061, 1063,
1069, 1087, 1091, 1093, 1097, 1103, 1109, 1117, 1123, 1129, 1151, 1153, 1163,
1171, 1181, 1187, 1193, 1201, 1213, 1217, 1223, 1229, 1231, 1237, 1249, 1259,
1277, 1279, 1283, 1289, 1291, 1297, 1301, 1303, 1307, 1319, 1321, 1327, 1361,
1367, 1373, 1381, 1399, 1409, 1423, 1427, 1429, 1433, 1439, 1447, 1451, 1453,
1459, 1471, 1481, 1483, 1487, 1489, 1493, 1499, 1511, 1523, 1531, 1543, 1549,
1553, 1559, 1567, 1571, 1579, 1583, 1597, 1601, 1607, 1609, 1613, 1619, 1621,
1627, 1637, 1657, 1663, 1667, 1669, 1693, 1697, 1699, 1709, 1721, 1723, 1733,
1741, 1747, 1753, 1759, 1777, 1783, 1787, 1789, 1801, 1811, 1823, 1831, 1847,
1861, 1867, 1871, 1873, 1877, 1879, 1889, 1901, 1907, 1913, 1931, 1933, 1949,
1951, 1973, 1979, 1987, 1993, 1997, 1999, 2003, 2011, 2017, 2027, 2029, 2039,
2053, 2063, 2069, 2081, 2083, 2087, 2089, 2099, 2111, 2113, 2129, 2131, 2137,
2141, 2143, 2153, 2161, 2179, 2203, 2207, 2213, 2221, 2237, 2239, 2243, 2251,
2267, 2269, 2273, 2281, 2287, 2293, 2297, 2309, 2311, 2333, 2339, 2341, 2347,
2351, 2357, 2371, 2377, 2381, 2383, 2389, 2393, 2399, 2411, 2417, 2423, 2437,
2441, 2447, 2459, 2467, 2473, 2477, 2503, 2521, 2531, 2539, 2543, 2549, 2551,
2557, 2579, 2591, 2593, 2609, 2617, 2621, 2633, 2647, 2657, 2659, 2663, 2671,
2677, 2683, 2687, 2689, 2693, 2699, 2707, 2711, 2713, 2719, 2729, 2731, 2741,
2749, 2753, 2767, 2777, 2789, 2791, 2797, 2801, 2803, 2819, 2833, 2837, 2843,
2851, 2857, 2861, 2879, 2887, 2897, 2903, 2909, 2917, 2927, 2939, 2953, 2957,
2963, 2969, 2971, 2999, 3001, 3011, 3019, 3023, 3037, 3041, 3049, 3061, 3067,
3079, 3083, 3089, 3109, 3119, 3121, 3137, 3163, 3167, 3169, 3181, 3187, 3191,
3203, 3209, 3217, 3221, 3229, 3251, 3253, 3257, 3259, 3271, 3299, 3301, 3307,
3313, 3319, 3323, 3329, 3331, 3343, 3347, 3359, 3361, 3371, 3373, 3389, 3391,
3407, 3413, 3433, 3449, 3457, 3461, 3463, 3467, 3469, 3491, 3499, 3511, 3517,
3527, 3529, 3533, 3539, 3541, 3547, 3557, 3559, 3571, 3581, 3583, 3593, 3607,
3613, 3617, 3623, 3631, 3637, 3643, 3659, 3671, 3673, 3677, 3691, 3697, 3701,
3709, 3719, 3727, 3733, 3739, 3761, 3767, 3769, 3779, 3793, 3797, 3803, 3821,
3823, 3833, 3847, 3851, 3853, 3863, 3877, 3881, 3889, 3907, 3911, 3917, 3919,
3923, 3929, 3931, 3943, 3947, 3967, 3989, 4001, 4003, 4007, 4013, 4019, 4021,
4027, 4049, 4051, 4057, 4073, 4079, 4091, 4093, 4099, 4111, 4127, 4129, 4133,
4139, 4153, 4157, 4159, 4177, 4201, 4211, 4217, 4219, 4229, 4231, 4241, 4243,
4253, 4259, 4261, 4271, 4273, 4283, 4289, 4297, 4327, 4337, 4339, 4349, 4357,
4363, 4373, 4391, 4397, 4409, 4421, 4423, 4441, 4447, 4451, 4457, 4463, 4481,
4483, 4493, 4507, 4513, 4517, 4519, 4523, 4547, 4549, 4561, 4567, 4583, 4591,
4597, 4603, 4621, 4637, 4639, 4643, 4649, 4651, 4657, 4663, 4673, 4679, 4691,
4703, 4721, 4723, 4729, 4733, 4751, 4759, 4783, 4787, 4789, 4793, 4799, 4801,
4813, 4817, 4831, 4861, 4871, 4877, 4889, 4903, 4909, 4919, 4931, 4933, 4937,
4943, 4951, 4957, 4967, 4969, 4973, 4987, 4993, 4999, 5003, 5009, 5011, 5021,
5023, 5039, 5051, 5059, 5077, 5081, 5087, 5099, 5101, 5107, 5113, 5119, 5147,
5153, 5167, 5171, 5179, 5189, 5197, 5209, 5227, 5231, 5233, 5237, 5261, 5273,
5279, 5281, 5297, 5303, 5309, 5323, 5333, 5347, 5351, 5381, 5387, 5393, 5399,
5407, 5413, 5417, 5419, 5431, 5437, 5441, 5443, 5449, 5471, 5477, 5479, 5483,
5501, 5503, 5507, 5519, 5521, 5527, 5531, 5557, 5563, 5569, 5573, 5581, 5591,
5623, 5639, 5641, 5647, 5651, 5653, 5657, 5659, 5669, 5683, 5689, 5693, 5701,
5711, 5717, 5737, 5741, 5743, 5749, 5779, 5783, 5791, 5801, 5807, 5813, 5821,
5827, 5839, 5843, 5849, 5851, 5857, 5861, 5867, 5869, 5879, 5881, 5897, 5903,
5923, 5927, 5939, 5953, 5981, 5987, 6007, 6011, 6029, 6037, 6043, 6047, 6053,
6067, 6073, 6079, 6089, 6091, 6101, 6113, 6121, 6131, 6133, 6143, 6151, 6163,
6173, 6197, 6199, 6203, 6211, 6217, 6221, 6229, 6247, 6257, 6263, 6269, 6271,
6277, 6287, 6299, 6301, 6311, 6317, 6323, 6329, 6337, 6343, 6353, 6359, 6361,
6367, 6373, 6379, 6389, 6397, 6421, 6427, 6449, 6451, 6469, 6473, 6481, 6491,
6521, 6529, 6547, 6551, 6553, 6563, 6569, 6571, 6577, 6581, 6599, 6607, 6619,
6637, 6653, 6659, 6661, 6673, 6679, 6689, 6691, 6701, 6703, 6709, 6719, 6733,
6737, 6761, 6763, 6779, 6781, 6791, 6793, 6803, 6823, 6827, 6829, 6833, 6841,
6857, 6863, 6869, 6871, 6883, 6899, 6907, 6911, 6917, 6947, 6949, 6959, 6961,
6967, 6971, 6977, 6983, 6991, 6997, 7001, 7013, 7019, 7027, 7039, 7043, 7057,
7069, 7079, 7103, 7109, 7121, 7127, 7129, 7151, 7159, 7177, 7187, 7193, 7207,
7211, 7213, 7219, 7229, 7237, 7243, 7247, 7253, 7283, 7297, 7307, 7309, 7321,
7331, 7333, 7349, 7351, 7369, 7393, 7411, 7417, 7433, 7451, 7457, 7459, 7477,
7481, 7487, 7489, 7499, 7507, 7517, 7523, 7529, 7537, 7541, 7547, 7549, 7559,
7561, 7573, 7577, 7583, 7589, 7591, 7603, 7607, 7621, 7639, 7643, 7649, 7669,
7673, 7681, 7687, 7691, 7699, 7703, 7717, 7723, 7727, 7741, 7753, 7757, 7759,
7789, 7793, 7817, 7823, 7829, 7841, 7853, 7867, 7873, 7877, 7879, 7883, 7901,
7907, 7919, 7927, 7933, 7937, 7949, 7951, 7963, 7993, 8009, 8011, 8017, 8039,
8053, 8059, 8069, 8081, 8087, 8089, 8093, 8101, 8111, 8117, 8123, 8147, 8161,
8167, 8171, 8179, 8191, 8209, 8219, 8221, 8231, 8233, 8237, 8243, 8263, 8269,
8273, 8287, 8291, 8293, 8297, 8311, 8317, 8329, 8353, 8363, 8369, 8377, 8387,
8389, 8419, 8423, 8429, 8431, 8443, 8447, 8461, 8467, 8501, 8513, 8521, 8527,
8537, 8539, 8543, 8563, 8573, 8581, 8597, 8599, 8609, 8623, 8627, 8629, 8641,
8647, 8663, 8669, 8677, 8681, 8689, 8693, 8699, 8707, 8713, 8719, 8731, 8737,
8741, 8747, 8753, 8761, 8779, 8783, 8803, 8807, 8819, 8821, 8831, 8837, 8839,
8849, 8861, 8863, 8867, 8887, 8893, 8923, 8929, 8933, 8941, 8951, 8963, 8969,
8971, 8999, 9001, 9007, 9011, 9013, 9029, 9041, 9043, 9049, 9059, 9067, 9091,
9103, 9109, 9127, 9133, 9137, 9151, 9157, 9161, 9173, 9181, 9187, 9199, 9203,
9209, 9221, 9227, 9239, 9241, 9257, 9277, 9281, 9283, 9293, 9311, 9319, 9323,
9337, 9341, 9343, 9349, 9371, 9377, 9391, 9397, 9403, 9413, 9419, 9421, 9431,
9433, 9437, 9439, 9461, 9463, 9467, 9473, 9479, 9491, 9497, 9511, 9521, 9533,
9539, 9547, 9551, 9587, 9601, 9613, 9619, 9623, 9629, 9631, 9643, 9649, 9661,
9677, 9679, 9689, 9697, 9719, 9721, 9733, 9739, 9743, 9749, 9767, 9769, 9781,
9787, 9791, 9803, 9811, 9817, 9829, 9833, 9839, 9851, 9857, 9859, 9871, 9883,
9887, 9901, 9907, 9923, 9929, 9931, 9941, 9949, 9967, 9973, 10007, 10009, 10037,
10039, 10061, 10067, 10069, 10079, 10091, 10093, 10099, 10103, 10111, 10133,
10139, 10141, 10151, 10159, 10163, 10169, 10177, 10181, 10193, 10211, 10223,
10243, 10247, 10253, 10259, 10267, 10271, 10273, 10289, 10301, 10303, 10313,
10321, 10331, 10333, 10337, 10343, 10357, 10369, 10391, 10399, 10427, 10429,
10433, 10453, 10457, 10459, 10463, 10477, 10487, 10499, 10501, 10513, 10529,
10531, 10559, 10567, 10589, 10597, 10601, 10607, 10613, 10627, 10631, 10639,
10651, 10657, 10663, 10667);

// index of the primes numbers
primeNumbers(index) = ba.take(index , list)
    with{
        list = primes;
        };
//process = primeNumbers(12);

//--------------------------------------------------------------------- BASIC --
// limit function for library and system
limit(maxl, minl, x) = max(minl, min(maxl, x));
//process = os.osc(200) : limit(.5, -.5);

// see signal values 
inspect(i, lower, upper) = _ : 
    (_ <: _, vbargraph("%i [style:numerical]", lower, upper) : attach);
//process = (os.osc(.01) : inspect(1, -1, 1));

// binary selector 0 - 1
selector(sel, x, y) = ( x * (1 - sel) + y * (sel) );
//process = selector(1, 10, 20);

//----------------------------------------------------------- INTERPOLATIONS ---
linInterpolate(x0, x1, delta) = x0 + delta * (x1-x0);
siglinInterpol(order, x) = x : seq(r, order, interpolate)
with{
    interpolate(y) = y + .5 * (y' - y);
};

bilinInterpolate(x0, x1, x0b, x1b, dt1, dt2) = 
    linInterpolate(
        linInterpolate(x0, x1, dTG1), 
        linInterpolate(x0b, x1b, dTG1),
        dt2)
    with{
        linInterpolate(x0, x1, delta) = x0 + delta * (x1-x0);
    };
// dTG1 = hslider("dt1",0,0,1,.001);
// dTG2 = vslider("dt2",0,0,1,.001);
// osc1 = (os.phasor(1,200) - .5) * 2;
// osc2 = ((os.phasor(1,200) > .5) - .5) * 2;
// osc3 = os.lf_triangle(200);
// osc4 = os.osc(200);
// process =   bilinInterpolate(osc1, osc2, osc3, osc4, dTG1, dTG2) : 
//             siglinInterpol(4, _) ;

//--------------------------------------------------------------- COVERSIONS ---
// Conversions Formula: dB to a linear gain (0-1) and linear to dB
linear2dB(G) = 20.0 * log10(max (ma.MIN, G));

// linear to dB
dB2linear(G) = pow(10.0, G/20.0);

// milliseconds to samples
ms2samp(t) = (t / 1000) * ma.SR;

// seconds to samples
sec2samp(t) = t * ma.SR;

// (samps) = give tot. samples we want to know in milliseconds
samps2ms(samps) = ((1000/ma.SR) * samps) : int;

// (meters) = give a distance in meters for samples of the filter
meters2samps(meters) = ((ma.SR/1000.) * ((1000*meters)/343.1) * 2);

// (samps,seconds) = give: samples of the filter, seconds we want for t60 decay
T60samps(samps, seconds) = 1/(10^((3*(((1000 / ma.SR)*samps)/1000))/seconds));

// (ms,seconds) = give: ms delay of the filter, seconds we want for t60 decay
T60ms(ms,seconds) = 1/(10^((3*(ms/1000))/seconds));


/************************************************************************
************************************************************************
- SIGNALS GENERATION -
************************************************************************
************************************************************************/

// Dirac Impulse at Compile Time
dirac = 1-1';

// pseudo-random noise with linear congruential generator (LCG)
noise(initSeed) = LCG ~ _ : (_ / m)
with{
    // initSeed = an initial seed value
    a = 18446744073709551557; // a large prime number, 
        // such as 18446744073709551557
    c = 12345; // a small prime number, such as 12345
    m = 2 ^ 31; // 2.1 billion
    // linear_congruential_generator
    LCG(seed) = ((a * seed + c) + (initSeed - initSeed') % m);
};

// noise with prime numbers seed
primeNoise(index) = primeNumbers(index+1) : noise;

// Stochastic Velvet Noise 
// unpredictable pulses dependent on an exponential curve on noise
// exponent for the curve, seed for the noise, ph 0 to 1
SVNoise(exponent, seed, ph) = circuit(exponent, seed, ph) 
with{
    noise(index) = (+ (index)~ * (1103515245)) / 2147483647;
    exponentialcurve(exponent,ph) = exponent ^ ((ph-.5) * 2) / exponent;
    comparator(exponent, ph, x) =  abs(x) > 
        ( (exponentialcurve(exponent,ph) * -1) +1) : \(z).(z > z');
    circuit(exponent, seed, ph) =  (ph > 0) *
        noise(seed) : comparator(exponent, ph);
};
//process =   SVNoise(100, 1429, hslider("SVNoiseph",0,0,1,.001));

// ramp generator: frequency, ramp in ms.
ramp(ms,trig) = (1000/ms)/ma.SR : + ~ _ * (1-trig) : \(x).( (x < 1) * x );

// pulse train (1 sample)
pulse(f) = f/ma.SR : + ~ \(x).( (x < 1) * x ) : \(y).( y < y' ) + (1-1');

// classic phasor
phasor(f) = (f / ma.SR) : (+ : decimal) ~ _ ;

// phasor with phase reset
phasorPH(f, trig) = (f/ma.SR):(+ :\(x).(selector(trig,x,0)) :\(x).(x-int(x)))~_ ;

// phasor with scattering of the phase when triggered (out 1),
// and splice of the phase = the trigger dimension (out 2)
// splicePH(fph, fsah, seed, width) = 
//     abs( SAH( (1 - pwm(fsah, width)) , 
//     noise(seed)) ) + 
//         phasor(fph) : decimal <: _, _ * pwm(fsah, width); 
// outputs: classic, PWM
// splicephasor(fph, fsah, seed) = splicePH(fph, fsah, seed, .5) : _, !;
// splicepwm(fph, fsah, seed, width) = splicePH(fph, fsah, seed, width) : !, _;

// phasor with scattering of the phase when triggered (out 1),
// and splice of the phase = the trigger dimension (out 2)
splicePH(frequency, noise, trigger) =
                                             ( PhasorPH(frequency,0) ),
        ( ((trigger*-1) + 1, primeNoise(noise)) : SAH : abs ):
        \(x,y).(
                    // out phasor + noise->SAH when triggered
                    (x+y : \(z).(z-int(z))),
                    // out phasor + noise->SAH * trigger
                    (x+y : \(z).(z-int(z)) * trigger)
                );

// sawtooth
saw(f) = (( (phasor(f) * -1) + 1) - .5) * 2;
//process = saw(100);

// sawtooth II
sawII(f) = ((phasor(f) - .5) * 2);
//process = sawII(100);

// sinusoidal oscillator
sinusoid(f) = phasor(f) : sine;
//process = sinusoid(100);

// sinusoidal oscillator M
sinusoidM(f) = phasor(f) * .5 : (sine - .5) * 2;
//process = sinusoidM(100);

// sinusoidal oscillator W
sinusoidW(f) = phasor(f) * .5 : (cosine + .5) * 2;
//process = sinusoidW(100);

// square
square(f) = ((phasor(f) > .5) - .5) * 2;
//process = square(100);

// PW Square, (PW = 0 to 1)
pwsquare(f, pw) = ((Phasor(f) * -1) + pw) > 0;
// PWM square - PWM control from -1 to +1
pwmsquare(f,pwm) = ((((phasor(f) - .5) * 2) + pwm > 0) - .5) * 2;
//process = pwmsquare(100, 0);

// triangular
triangular(f) = phasor(f) : (triangle - .5) * 2;
//process = triangular(100);

// button counter
buttoncounter(N) = button("PRGRMChange") : \(x).((x - x') > 0 : (+ ~ _ )%(N));


/************************************************************************
************************************************************************
- CHAOTIC OSCILLATORS -
************************************************************************
************************************************************************/

/*
Logistic Map
*/
logisticmap(x0,r) = circuit ~ _
    with {
        circuit(y) = (x0-x0') + y * r * (1 - y);
    };
// process = (.8, 3.99991) : logisticmap -.5 : \(x).(x,x);

/*
Sine Map
*/
sinemap(x0,s) = circuit ~ _
    with {
        circuit(x) = (x0-x0') + s * sin(ma.PI * x);
    };
// process = (.5,.9992) : sinemap -.5 : \(x).(x,x);

henonmap(a,b) = circuit ~ _
    with {
        circuit(x) = 1 - (a * (x * x)) + (b * x');
    };
//process = (1.4,0.3) : henonmap : \(x).(x,x);

/*
First Return Map
*/
firstreturnmap(x0,u) = circuit ~ _
    with {
        circuit(x) = (x0-x0') + 4 * u * x * (1 - x);
    };
// process = (.5,.9992) : firstreturnmap;

/*
Collatz conjecture
*/
collatz(x0) = circuit ~ _
    with {
        circuit(w) = (x0 - x0') + w :
                        \(x).((x%(2) == 0) * x, (x%(2) == 1) * x) :
                            \(odd,even).(odd/2,(even*3)+((even>0))) :> +;
    };
// process = sin( (collatz(139489332) * ma.PI));

/*
Bernoulli Shift
*/
bernoulli(x0) = circuit ~ _ : \(z).(z-int(z))
    with {
        circuit(x) = (x0 - x0') + (x * 2.0);
    };
// process = bernoulli(.1324);

/*
Lorenz Model
*/
lorenz(x0,y0,z0,sigma,rho,beta,dt,q) = circuit ~ si.bus(3)
    with {
        circuit(x,y,z) =
            ( (x+ (sigma*(y-x))*dt) * q )+ (x0-x0'),
            ( (y+ (rho*x -(x*z) -y) * dt)*q )+ (y0-y0'),
            ( (z+ ((x*y)-(beta*z)) * dt)*q )+ (z0-z0');
    };
/*
process =   (1.2,1.3,1.6, 10.0,28,2.666667,.005, 1.0) :
                lorenz : \(x,y,z).(x+y+z)/100 : \(x).(x,x);
                */

/*
Van Der Pol Oscillator
*/
vanderpol(x0,y0,u,dt,epsilon) = circuit ~ si.bus(2)
    with {
        circuit(x,y) =
            ( x + (u * (x - x ^ 3 / 3 - y) * dt) ) + (x0-x0'),
            ( y + (x / max(u,epsilon) ) * dt ) + (y0-y0');
    };
/*
process = (1,1, 1.0,0.01,2.2204460492503131e-016) :
            vanderpol : \(x,y).(x/4,y/4);
            */

/*
Chua Oscillator
*/
chua(x0,y0,z0,k,a,b,alpha,beta,gamma,dt) = circuit ~ si.bus(3)
    with {
        f(x) = b * x + .5 * (a-b) * (abs(x + 1) - abs(x - 1));
        circuit(x,y,z) =
            (x0-x0' + x + k *( alpha * (y - x - f(x))) * dt),
            (y0-y0' + y + k *( (x - y + z)) * dt),
            (z0-z0' + z + k *(-beta * y - gamma * z) * dt);
    };
/*
process =   (1.16346, -0.0972335, -0.905656, 1, -1.14286,
            -0.714286, 15.6, 28.58, 0, 0.01) :
                chua : \(x,y,z).(x+y+z)/3 : \(x).(x,x);
                */


/*
for more models serch:
    -Dyadic transformation
    -Gauss iterated map
    -Bogdanov map
    -Brusselator
    -Lotka-Volterra
    -Duffing Oscillator
    -Hindmarsh–Rose neuron model
*/


/************************************************************************
************************************************************************
- FILTERS -
************************************************************************
************************************************************************/

//-------------------------------------------- DELAY with FB 1 sample Correction
DEL(del, x) = x@(del - 1);

//--------------------------------------------- ONEZERO FILTER (FIR of I° Order)
// (g) = give amplitude 0-1(open-close) to the delayed signal 
// (g) = +1 lowpass, (g) = -1 highpass
ozf(g, x) = (x' * g), x :> +;
//process = no.noise : ozf(hslider("HP/LP",0, -1, +1, .001));

//--------------------------------------- ONEPOLE FILTER (IIR of 1 sample delay)
// (g) = give amplitude 1-0(open-close) for the lowpass cut
opf(g, x) = x * g : + ~ (_ : * (1 - g));
// smooth function (Onepole Filter)
smooth(G) = *(1-G) : + ~ *(G);
// only the pole section
pole(g) = + ~ * (g);
//process = no.noise : opf(.01);
//process = no.noise * .001 : op(.99);

// With Frequency Cut
// (G)  = give amplitude 1-0 (open-close) for the lowpass cut
// (CF) = Frequency Cut in HZ
onepole(CF, x) = loop ~ _ 
    with{
        g(x) = x / (1.0 + x);
        G = tan(CF * ma.PI / ma.SR):g;
        loop(y) = x * G + (y * (1 - G));
        };
//process = no.noise : onepole(10);

// With Tau Decay
OPFtau(tau, x) = fb ~ _ 
with {  
    fb(y) = (1.0 - s) * x + s * y; 
    s = exp(-1.0/(tau * ma.SR));
    //   tau = desired smoothing time constant in seconds
};

//------------------------------------ FEEDFORWARD COMB FILTER (FIR of N° Order)
// (t, g) = delay time in samples, filter gain 0-1
ffcf(t, g, x) = (x@(t) * g), x :> +;
//process = no.noise * .1 : ffcf(100, 1);

//--------------------------------------- FEEDBACK COMB FILTER (FIR of N° Order)
// (del, g) = DEL=delay time in samples. G=feedback gain 0-1
fbcf(del, g, x) = loop ~ _ 
    with {
        loop(y) = x + y@(del - 1) * g;
    };
//process = no.noise * .1 : fbcf(4480, .9);

//------------------------------- LOWPASS FEEDBACK COMB FILTER (FIR of N° Order)
// LPFBC(Del, FCut) = give: delay samps, -feedback gain 0-1-, lowpass Freq.Cut HZ
lpfbcf(del, cf, x) = loop ~ _ : !, _
    with {
        onepole(CF, x) = loop ~ _ 
            with{
                g(x) = x / (1.0 + x);
                G = tan(CF * ma.PI / ma.SR):g;
                loop(y) = x * G + (y * (1 - G));
            };
        loop(y) = x + y@(del - 1) <: onepole(cf), _;
    };
//process = _ * .1 : lpfbcf(2000, 10000);

//--------------------------------------------------------------- ALLPASS FILTER
// (t, g) = give: delay in samples, feedback gain 0-1
apf(del, g, x) = x : (+ : _ <: @(del  - 1), *(g))~ *(-g) : mem, _ : + : _;

nestapf(del, g, y) = _ : (+ : _ <: @(del  - 1), * (g))~ y *(-g) : mem, _ : + : _;
// process = _ : 
//     nestapf(ms2samp(40), .5, 
//         nestapf(ms2samp(30), .5, 
//             nestapf(ms2samp(20), .75, _)));

schroederapf(N, del, g) = seq(i, N, _ : apf(del, g));
//process = schroederapf(10, ms2samp(100), .5);
//----------------------------------------------------- MODULATED ALLPASS FILTER
// Modulated Allpass filter
modapf(delsamples, samplesmod, freqmod, apcoeff) = ( + : _ <: 
    delayMod(delsamples, samplesmod, freqmod),
    * (apcoeff))~ * (-apcoeff) : mem, _ : + : _
    with{
        delayMod(samples, samplesMod, freqMod, x) = delay
        with{
            unipolarMod(f, samples) = ((os.osc(f) + 1) / 2) * samples;
            delay = x : de.fdelay(samples, samples - unipolarMod(freqMod, samplesMod));
        };
    };
//process = 1-1' : +@(ma.SR/100) ~ _ <: _, modapf(1000, 500, .12, .5);

//---------------------------------------------------------------------- SVF TPT
// SVFTPT filter function
// TPT version of the SVF Filter by Vadim Zavalishin
// reference : (by Will Pirkle)
// http://www.willpirkle.com/Downloads/AN-4VirtualAnalogFilters.2.0.pdf
SVFTPT(Q, cf, x) = loop ~ si.bus(2) : (! , ! , _ , _ , _ , _ , _)
    with {
        g = tan(cf * ma.PI * ma.T);
        R = 1.0 / (2.0 * Q);
        G1 = 1.0 / (1.0 + 2.0 * R * g + g * g);
        G2 = 2.0 * R + g;
        loop(s1, s2) = u1 , u2 , lp , hp , bp * 2.0 * R , x - bp * 4.0 * R , bp
            with {
                hp = (x - s1 * G2 - s2) * G1;
                v1 = hp * g;
                bp = s1 + v1;
                v2 = bp * g;
                lp = s2 + v2;
                u1 = v1 + bp;
                u2 = v2 + lp;
            };
    };

LPSVF(Q, cf, x) = SVFTPT(Q, cf, x) : (_ , ! , ! , ! , !);

HPSVF(Q, cf, x) = SVFTPT(Q, cf, x) : (! , _ , ! , ! , !);

BPSVF(Q, cf, x) = SVFTPT(Q, cf, x) : (! , ! , _ , ! , !);

NotchSVF(Q, cf, x) = x - BPSVF(Q, cf, x);

APSVF(Q, cf, x) = SVFTPT(Q, cf, x) : (! , ! , ! , _ , !);

PeakingSVF(Q, cf, x) = LPSVF(Q, cf, x) - HPSVF(Q, cf, x);

BP2SVF(Q, cf, x) = SVFTPT(Q, cf, x) : (! , ! , ! , ! , _);

BPBWSVF(BW, CF, x) = BPSVF(limit(20000, minimum, (CF / BW)), CF, x);
// process = BPBWSVF(10, 1000, noise(1234));

// Usa .707 come Q per LP e HP. BPSVF è quello normalizzato.
//process = no.noise : BPSVF(10, 1000);

// Zavalishin's SVF BP FILTER
// optimized BP from the TPT version of the SVF Filter by Vadim Zavalishin
// reference : (by Will Pirkle)
// http://www.willpirkle.com/Downloads/AN-4VirtualAnalogFilters.2.0.pdf
BPSVFTPT(glin, bw, cf, x) = loop ~ si.bus(2) : (! , ! , _)
    with {
        g = tan(cf * ma.PI * (1.0/ma.SR));
        Q = cf / max(ma.EPSILON, bw);
        R = 1.0 / (Q + Q);
        G = 1.0 / (1.0 + 2.0 * R * g + g * g);
        loop(s1, s2) = u1 , u2 , bp * glin
            with {
                bp = (g * (x - s2) + s1) * G;
                bp2 = bp + bp;
                v2 = bp2 * g;
                u1 = bp2 - s1;
                u2 = v2 + s2;
            };
    };

//------------------------------------------------------------------ ONEPOLE TPT
// Zavalishin Onepole TPT Filter
// TPT version of the One-Pole Filter by Vadim Zavalishin
// reference : (by Will Pirkle)
// http://www.willpirkle.com/Downloads/AN-4VirtualAnalogFilters.2.0.pdf
onePoleTPT(cf, x) = loop ~ _ : ! , si.bus(3)
    with {
        g = tan(cf * ma.PI * ma.T);
        G = g / (1.0 + g);
        loop(s) = u , lp , hp , ap
            with {
            v = (x - s) * G; u = v + lp; lp = v + s; hp = x - lp; ap = lp - hp;
            };
    };

// Lowpass  TPT
LPTPT(cf, x) = onePoleTPT(cf, x) : (_ , ! , !);

// Highpass TPT
HPTPT(cf, x) = onePoleTPT(cf, x) : (! , _ , !);

// Allpass TPT
APTPT(cf, x) = onePoleTPT(cf, x) : (!, !, _);

//-------------------------------------------------------------- BUTTERWORTH ---
butterworthQ(order, stage) = qFactor(order % 2)
    with {
        qFactor(0) = 1.0 / (2.0 * cos(((2.0 * stage + 1) *
        (ma.PI / (order * 2.0)))));
        qFactor(1) = 1.0 / (2.0 * cos(((stage + 1) * (ma.PI / order))));
    };

LPButterworthN(1, cf, x) = LPTPT(cf, x);
LPButterworthN(N, cf, x) = cascade(N % 2)
    with {
        cascade(0) = x : seq(i, N / 2, LPSVF(butterworthQ(N, i), cf));
        cascade(1) = x : LPTPT(cf) : seq(i, (N - 1) / 2,
        LPSVF(butterworthQ(N, i), cf));
    };
// process = LPButterworthN(4, 1000, noise(1234));

HPButterworthN(1, cf, x) = HPTPT(cf, x);
HPButterworthN(N, cf, x) = cascade(N % 2)
    with {
        cascade(0) = x : seq(i, N / 2, HPSVF(butterworthQ(N, i), cf));
        cascade(1) = x : HPTPT(cf) : seq(i, (N - 1) /
        2, HPSVF(butterworthQ(N, i), cf));
    };
// process = HPButterworthN(4, 10000, noise(1234));
    
//------------------------------------------------------------------- DC BLOCKER
// https://ccrma.stanford.edu/~jos/fp/DC_Blocker.html
dcblocker(x) = x : ozf(-1) : pole(0.998);


/************************************************************************
************************************************************************
- AUDIO INFORMATION PROCESSING -
************************************************************************
************************************************************************/

//----------------------------------------------------------------- MAX PEAK ---
// references: 1, 2
// Peak Max with IIR filter and max comparison
peakmax(x) = loop ~_
with{
    loop(y) = abs(x) , y : max;
};
//process = _ : peakmax;

//--------------------------------------------------- MAX PEAK NORMALIZATION ---
// references: 5
// Peak Max Normalization
fixedNorm(x) = 1 / (x : loop ~ _) * x
with{
    loop(y,z) = ( (y, abs(z) ) : max);
};
//process = _ * .2 : fixedNorm;

//--------------------------------------------------- PEAK ENVELOPE FOLLOWER ---
// references: 1, 2
// Peak Envelope Follower with t60 Decay
peakenvelope(t60, x) = abs(x) <: loop ~ _ * rt60(t60)
    with{
        loop(y,z) = y, z : max;
        rt60(t) = 0.001 ^ ((1/ma.SR) / t);
    };
//process = peakenvelope(10);

//---------------------------------------------- PEAK ENVELOPE NORMALIZATION ---
// Fixed Peak Normalization with Decay
peakNorm(seconds, x) = 1 / peakenvelope(seconds, x) * x;

//----------------------------------------- MOVING AVERAGE ENVELOPE FOLLOWER ---
movingAverage(seconds, x) = x - x@(seconds * ma.SR) : 
    fi.pole(1.0) / (seconds * ma.SR);
//process = _ * 10 : movingAverage(1);

//------------------------------------------------------- MOVING AVERAGE RMS ---
movingAverageRMS(seconds, x) = sqrt(max(0, movingAverage(seconds, x ^ 2)));
//process = movingAverageRMS(1);

//----------------------------------------------- ATTACK DECAY - RMS AVERAGE ---
// references: 3
RMS(att, rel, x) = loop ~ _ : sqrt
    with {
        loop(y) = (1.0 - coeff) * x * x + coeff * y
            with {
                attCoeff = exp(-2.0 * ma.PI * ma.T / att);
                relCoeff = exp(-2.0 * ma.PI * ma.T / rel);
                coeff = ba.if(abs(x) > y, attCoeff, relCoeff);
            };
    };
//process = os.osc(200) : RMS(10, 10);

//----------------------------------------- ATTACK DECAY - RMS NORMALIZATION ---
RMSNorm(att, dec, target, delSec, x) = 
    (target / max(ma.EPSILON, RMS(att, dec, x))) * 
        x@(ma.SR * delSec);  
//process = os.osc(200) : RMSNorm(10, 10, .1); 
//process = _ : + ~ RMSNorm(10, 10, .1)@10000 * .99; 

RMSNormi(i, att, dec, target, delSec, x) = 
    (target / 
        (max(ma.EPSILON, RMS(att, dec, x))) : _
            <: _, vbargraph("RMS %i [style:numerical]", 0, 1) : attach) * 
                x@(ma.SR * delSec);

//-------------------------------------------------------------- PEAK HOLDER ---
/*
peakHolder(secondsPeriod, x) = y
    letrec {
        'y = ba.if(reset, abs(x), max(y, abs(x)));
    }
        with {
            reset = os.phasor(1, 1.0 / secondsPeriod) : \(x).(x < x');
        };
*/

// holdTime in Seconds
peakHolder(holdTime, x) = loop ~ si.bus(2) : ! , _
with {
    loop(timerState, outState) = timer , output
    with {
        isNewPeak = abs(x) >= outState;
        isTimeOut = timerState >= (holdTime * ma.SR - 1);
        bypass = isNewPeak | isTimeOut;
        timer = ba.if(bypass, 0, timerState + 1);
        output = ba.if(bypass, abs(x), outState);
    };
};
//process = _ : peakHolder(2);

//------------------------------------------------------- LOOKAHEAD LIMITING ---
// references: 1, 4
// peak-hold module with an exponential decay curve
peakHoldwDecay(holdSeconds, frequencyCut, decayT60, x) = 
    x : peakHolder(holdSeconds) : 
        LPTPT(frequencyCut) : peakenvelope(decayT60);

lookaheadNorm(treshold, x) = treshold / ( x : peakHoldwDecay(.1, 500, 10) : 
    max(ma.EPSILON, _) : min(1.0) ) * x @ (( 2/1000 ) * ma.SR);

lookaheadLimiter(threshold, x) = ( x : peakHoldwDecay(.1, 500, 10) ) : 
    ( threshold / max(ma.EPSILON, _) ) *
        x @ (( 2/1000 ) * ma.SR); 

//------------------------------------------------------------ PITCH TRACKER ---
// averaging function with 2pi time constant; t is the averaging time in seconds
avg(t, x) = y
    letrec {
        'y = x + alpha * (y - x);
    }
        with {
            alpha = exp((-2.0 * ma.PI * ma.T) / t);
        };

// zero-crossing indicator function
zeroCrossing(x) = (x * x') < 0;

// zero-crossing rate
zeroCrossingRate(t, x) = zeroCrossing(x) : avg(t);

// pitch tracker as zero-crossing rate of self-regulating lowpassed inputs
// we highpass the input to avoid infrasonic
// components to affect the measurements
// we then clip the lowpass cutoff to improve stability
pitchTracker(t, x) = loop ~ _
    with {
        loop(y) = fi.lowpass(4, max(80, y), xHighpassed) :
                  (zeroCrossingRate(t) * ma.SR * .5)
            with {
                xHighpassed = fi.highpass(1, 20, x);
            };
    };
//process = os.osc(1000) : pitchTracker(.5);

//---------------------------------------------------------------- LOCAL MAX ---
localMax(seconds, x) = loop ~ si.bus(4) : _ , ! , ! , !
with {
    loop(yState, timerState, peakState, timeInSamplesState) = 
        y , timer , peak , timeInSamples
    with {
        timeInSamples = ba.if(reset + 1 - 1', limit(1000, 0, seconds) * 
            ma.SR, timeInSamplesState);
        reset = timerState >= (timeInSamplesState - 1);
        timer = ba.if(reset, 1, timerState + 1);
        peak = max(abs(x), peakState * (1.0 - reset));
        y = ba.if(reset, peak', yState);
    };
};
// process = localMax(4);

//---------------------------------------------------------- SAMPLE AND HOLD ---
// SAH circuit
SAH(ph, y) = \(FB).( selector( ph : \(x).(x < x'), FB, y ) )~ _ ;

// SAH with internal trigger
SAH2(f, y) = \(FB).( selector( phasor(f) : \(x).(x < x'), FB, y ) )~ _ ;


/************************************************************************
************************************************************************
- SAMPLERS -
************************************************************************
************************************************************************/

//----------------------------------------------------------- OLA GRANULATOR ---
// Timestretcher - sum of the 2 Head Reads
// Bufpos = 0 to 1 signals for the reads
timeStretcher(bufferMax, bufferSec, record, readSection, readOffset, readStretch, readJitter, grainDimMs, i, x) = x <: head1 + head2 
with{
    offset = 2;
    // tableMax = table Max Dimension - 10 Seconds
    tableMax = 192000 * bufferMax + offset;

    // Phasor Function
        LetrecPhasor(f) = Xn
            letrec{
                'Xn = (Xn+(f/ma.SR)) : frac;
            };

    frac(x) = x-floor(x);
    // read section
    // stretchFactor - 0 Normal / 1 Extreme stretch (Freeze)
        stretch = LetrecPhasor(1-readStretch)
            * readSection;
    // Jitter Amount in the position for the reads
    jitter = no.noise * readJitter;
    // position in the Buffer for the Reads
    cntrlRead = readOffset+stretch+jitter : frac;
    // L = buffer dimension in seconds
    L = ma.SR * bufferSec;
    // Write index - ramp 0 to L
    wIdx = offset + ((+(1) : %(L)) ~ _) * record : int <: 
        attach(_, _/L : hbargraph("[0]Write-Head %i",0,1));
    buffer(p, x) = it.frwtable(3, tableMax, .0, wIdx, x, p);
    // Hanning window Equation
    hann(x) = sin(frac(x) * ma.PI) ^ 2.0;
    // Grain in Milliseconds
    grainms = 1000/grainDimMs : si.smoo;
    // Position of the grain in the Buffer
    bufPos = cntrlRead <: attach(_, _ : hbargraph("[1]Read-Head %i",0,1));
    timePhase = offset + (bufPos * L);
    // two Heads for the read
    // 0°
    ph1 = LetrecPhasor(grainms);
    // 180* 
    ph2 = ma.frac(.5 + ph1);
    // Buffer positions = Position in the Buffer + Grain Read
    pos1 = SAH(ph1, timePhase) + ph1*(ma.SR/grainms);
    pos2 = SAH(ph2, timePhase) + ph2*(ma.SR/grainms);
    // Windows + Buffer Reads
    head1 = hann(ph1) * buffer(pos1);
    head2 = hann(ph2) * buffer(pos2);
};
// process = timeStretcher(1, 1, 1, .1, .5, .1, .01, 80, 1), 
//     timeStretcher(1, 1, 1, .1, .0, .1, .01, 80, 2);

sampler(memSec, readSpeed, x) =
    it.frwtable(
                    3, // interpolation order
                    192000 * (20), // max.SR * Seconds
                    .0, // start Point
                    ba.period(memSec * ma.SR), // writing index
                    x, // input
                    phasor(readSpeed/memSec) *
                      memSec * ma.SR// external Reading index (0-1)
                );

chunkSampler(memSec, readSpeed, jumpFreq, chunkDim, seed, x) =
    sampler * squareOut
        with{
            square = pwsquare(jumpFreq, chunkDim);
            reader =  splicePH(readSpeed/memSec, seed, square);
            readOut = reader : _, !, !;
            squareOut = reader : !, _, !;
            sampler =
                it.frwtable(
                    3, // interpolation order
                    192000 * (20), // max.SR * Seconds
                    .0, // start Point
                    ba.period(memSec * ma.SR), // writing index
                    x, // input
                    readOut * memSec * ma.SR// external Reading index (0-1)
                );
        };
//process = chunkSampler(4, 1, 8, .99, 2), chunkSampler(4, 1, 8, .99, 4);

chunkGrains(voices, memSec, readSpeed, jumpFreq, chunkDim, x)=
    par(i, voices,
            chunkSampler(memSec, readSpeed, jumpFreq, chunkDim, i, x/voices)
        ) :> (+,+);
//process = _ <: chunkGrains(8, 4, 1, 4, .99);
//pwg = hslider("slide",0,0,1,.001);
//ftg = hslider("freq.",0,0,100,.001);